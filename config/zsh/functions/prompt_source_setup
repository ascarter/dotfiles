#autoload
prompt_source_help() {
  cat <<-EOF
Source prompt with optional git support.
EOF
}

prompt_source_setup() {
  emulate -L zsh
  local -a opt_git
  zparseopts -git=opt_git

  prompt_opts=(cr percent subst)

  supports_unicode() {
    # Prefer LC_ALL, then LC_CTYPE, then LANG
    local loc="${LC_ALL:-${LC_CTYPE:-$LANG}}"

    # Case-insensitive match for UTF-8 / UTF8
    [[ $loc == *[Uu][Tt][Ff]-8* || $loc == *[Uu][Tt][Ff]8* ]]
  }

  if supports_unicode; then
    typeset -g prompt_symbol="â¯"
    typeset -g root_symbol="#"
  else
    typeset -g prompt_symbol=">"
    typeset -g root_symbol="#"
  fi

  # Update terminal/tab title on each prompt render
  function _title_ssh() {
    if [[ -n $SSH_CONNECTION ]]; then
      print -Pn "%n@%m:"
    fi
  }

  function _title_preexec() {
    local cmd="${1%% *}"
    local rest="${1#"$cmd"}"
    print -Pn "\e]0;"
    _title_ssh
    print -Pn "${cmd}${rest}\a"
  }

  function _title_precmd() {
    print -Pn "\e]0;"
    _title_ssh
    print -Pn "${PWD/#$HOME/~}\a"
  }

  PROMPT=

  # Set up title updating
  case "$TERM_PROGRAM" in
    ghostty|zed)
      ;;
    *)
      precmd_functions+=(_title_precmd)
      preexec_functions+=(_title_preexec)
      ;;
  esac

  # User and host for SSH sessions
  if [[ -n $SSH_CONNECTION ]]; then
    PROMPT+="[%B%n@%m%b] "
  fi

  # Working directory
  PROMPT='%B%2~%b'

  # Optional git branch info
  if (( $#opt_git )); then
    autoload -Uz vcs_info add-zsh-hook
    add-zsh-hook precmd vcs_info
    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:git:*' formats '%b'
    zstyle ':vcs_info:git*:*' actionformats '%b|%a'
    zstyle ':vcs_info:*' check-for-changes false
    PROMPT+='${vcs_info_msg_0_:+ "%F{7}$vcs_info_msg_0_%f"}'
  fi

  # Prompt symbol
  PROMPT+=' %(!.%F{red}${root_symbol}%f.${prompt_symbol}) '
}

prompt_source_setup "$@"
